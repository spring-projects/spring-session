buildscript {
	ext {
		releaseBuild = version.endsWith('RELEASE')
		snapshotBuild = version.endsWith('SNAPSHOT')
		milestoneBuild = !(releaseBuild || snapshotBuild)

		springBootVersion = '2.5.3'
	}

	repositories {
		gradlePluginPortal()
		maven { url 'https://repo.spring.io/plugins-release/' }
        maven {
            url = 'https://repo.spring.io/plugins-snapshot'
            if (project.hasProperty('artifactoryUsername')) {
                credentials {
                    username "$artifactoryUsername"
                    password "$artifactoryPassword"
                }
            }
        }
	}

	dependencies {
		classpath 'io.spring.gradle:spring-build-conventions:0.0.37'
		classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
	}
}

plugins {
	id "io.github.rwinch.antora" version "0.0.2"
}


apply plugin: 'io.spring.convention.root'

group = 'org.springframework.session'
description = 'Spring Session'

antora {
    playbookFile = file("local-antora-playbook.yml")
    // default no version (current version)
    antoraVersion = "3.0.0-alpha.9"
    arguments = ["--fetch"]
}

subprojects {
	apply plugin: 'io.spring.javaformat'

	plugins.withType(JavaPlugin) {
		sourceCompatibility = JavaVersion.VERSION_1_8
	}

	tasks.withType(Test) {
		useJUnitPlatform()
	}
}
