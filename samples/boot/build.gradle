buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
	}
}

apply plugin: 'org.springframework.boot'

apply from: JAVA_GRADLE
apply from: SAMPLE_GRADLE

group = 'samples'

ext['spring-security.version'] = springSecurityVersion

dependencies {
	compile project(':spring-session'),
			"org.springframework.boot:spring-boot-starter-data-redis",
			"org.springframework.boot:spring-boot-starter-web",
			"org.springframework.boot:spring-boot-starter-thymeleaf",
			"org.springframework.boot:spring-boot-starter-security",
			"nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect",
			"org.thymeleaf.extras:thymeleaf-extras-conditionalcomments",
			"org.webjars:bootstrap:$bootstrapVersion",
			"org.webjars:html5shiv:$html5ShivVersion",
			"org.webjars:webjars-locator"

	testCompile "org.springframework.boot:spring-boot-starter-test"

	integrationTestCompile gebDependencies,
			"org.spockframework:spock-spring:$spockVersion"

}

integrationTest {
	doFirst {
		def port = reservePort()

		def host = 'localhost:' + port
		systemProperties['geb.build.baseUrl'] = 'http://'+host+'/'
		systemProperties['geb.build.reportsDir'] = 'build/geb-reports'
		systemProperties['server.port'] = port
		systemProperties['management.port'] = 0

		systemProperties['spring.session.redis.namespace'] = project.name
	}
}

def reservePort() {
	def socket = new ServerSocket(0)
	def result = socket.localPort
	socket.close()
	result
}
