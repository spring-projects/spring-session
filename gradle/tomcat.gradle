buildscript {
	repositories {
		maven { url "https://repo.spring.io/plugins-release" }
	}
	dependencies {
		classpath("com.bmuschko:gradle-tomcat-plugin:2.2.5")
	}
}

apply plugin: 'war'
apply plugin: 'com.bmuschko.tomcat'

[tomcatRun,tomcatRunWar]*.contextPath = '/'


task integrationTomcatRun(type: com.bmuschko.gradle.tomcat.tasks.TomcatRun) {
	onlyIf { !sourceSets.integrationTest.allSource.empty }
	contextPath = tomcatRun.contextPath
	daemon = true
	tomcatClasspath = tomcatRun.tomcatClasspath
	webAppClasspath = tomcatRun.webAppClasspath
	webAppSourceDirectory = tomcatRun.webAppSourceDirectory
	doFirst {
		def mainOutputDir = project.sourceSets.main.output.classesDir
		if(mainOutputDir) {
			classesDirectory = mainOutputDir
		}
		// delay reserving ports to ensure they are still available
		def ports = reservePorts(3)
		httpPort = ports[0]
		ajpPort = ports[1]
		stopPort = ports[2]

		System.setProperty('spring.session.redis.namespace',project.name)
	}
}

task integrationTomcatStop(type: com.bmuschko.gradle.tomcat.tasks.TomcatStop) {
	onlyIf { !sourceSets.integrationTest.allSource.empty }
	doFirst {
		stopPort = integrationTomcatRun.stopPort
	}
}

integrationTest {
	dependsOn integrationTomcatRun
	doFirst {
		systemProperties['tomcat.port'] = integrationTomcatRun.httpPort
	}
	finalizedBy integrationTomcatStop
}

def reservePorts(int count) {
	def sockets = []
	for(int i in 1..count) {
		sockets << new ServerSocket(0)
	}
	def result = sockets*.localPort
	sockets*.close()
	result
}
